buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group = 'eu.ydp'
def projectName = 'mathplayer'

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/youngdigitalplanet/empiria'
        }
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
}

jar {
    baseName projectName
    from sourceSets.main.java.srcDirs
    from sourceSets.main.output
    duplicatesStrategy 'exclude'
}

dependencies {
    compile group: 'com.google.gwt', name: 'gwt-servlet', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-dev', version: '2.7.0'

    compile group: 'org.ow2.asm', name: 'asm', version: '5.0.3'
    compile group: 'org.ow2.asm', name: 'asm-util', version: '5.0.3'
    compile group: 'org.ow2.asm', name: 'asm-commons', version: '5.0.3'
    compile group: 'eu.ydp', name: 'ydpgwtutils', version: '2.0.1'

    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.0'
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId projectName
            version version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['BintrayPublication']
    pkg {
        repo = 'empiria'
        name = projectName
        userOrg = 'youngdigitalplanet'
        licenses = ['EPL-1.0']
        vcsUrl = 'https://github.com/YoungDigitalPlanet/mathplayer'
        publicDownloadNumbers = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
